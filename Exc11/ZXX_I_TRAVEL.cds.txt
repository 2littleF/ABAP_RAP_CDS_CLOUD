@AccessControl.authorizationCheck: #NOT_REQUIRED
@EndUserText.label: 'ZXX_TRAVEL'
@Metadata.ignorePropagatedAnnotations: false
define root view entity ZXX_I_TRAVEL
  as select from zdmotravel_xx
  association [0..1] to /DMO/I_Agency            as _Agency        on $projection.AgencyID = _Agency.AgencyID
  association [0..1] to /DMO/I_Customer          as _Customer      on $projection.CustomerID = _Customer.CustomerID
  association [1..1] to ZXX_I_OVERALL_STATUS_VH as _OverallStatus on $projection.OverallStatus = _OverallStatus.OverallStatus
  association [0..1] to I_Currency               as _Currency      on $projection.CurrencyCode = _Currency.Currency

{
  key travel_id      as TravelID,
      agency_id      as AgencyID,
      customer_id    as CustomerID,
      begin_date     as BeginDate,
      end_date       as EndDate,
      @Semantics.amount.currencyCode: 'CurrencyCode'
      booking_fee    as BookingFee,
      @Semantics.amount.currencyCode: 'CurrencyCode'
      total_price    as TotalPrice,
      currency_code  as CurrencyCode,
      description    as Description,
      overall_status as OverallStatus,
      @Semantics.user.createdBy: true
      createdby      as Createdby,
      @Semantics.systemDateTime.createdAt: true
      createdat      as Createdat,
      @Semantics.user.lastChangedBy: true
      lastchangedby  as Lastchangedby,
      @Semantics.systemDateTime.lastChangedAt: true
      lastchangedat  as Lastchangedat,
      @Semantics.systemDateTime.localInstanceLastChangedAt: true
      loc_changed_at as LocalChangedAt,
      _Customer,
      _Agency,
      _OverallStatus,
      _Currency
}
